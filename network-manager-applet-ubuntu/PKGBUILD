# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase%-*}, repo=yakkety
# vercheck-archlinux: name=${pkgbase%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgbase%-*}, majorver=1.0

pkgbase=network-manager-applet-ubuntu
pkgname=(network-manager-applet-ubuntu libnm-gtk-ubuntu)
_ubuntu_rel=0ubuntu1
_ubuntu_ver=1.2.2
pkgver=1.2.2
pkgrel=1
pkgdesc="Applet for managing network connections"
arch=(i686 x86_64)
url="http://www.gnome.org/projects/NetworkManager/"
license=(GPL LGPL2.1)
makedepends=(libnm-glib libsecret gtk3-ubuntu gnome-icon-theme
             mobile-broadband-provider-info iso-codes libnotify libmm-glib 
             intltool gobject-introspection python2)
extrafiles=(network-manager-applet.install)
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgbase%-*}/${pkgver%.*}/${pkgbase%-*}-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase%-*}_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
sha512sums=('1a763acc045b56f56d1faba17ec1523969f9b202a611bef7a52a55c3658a29acf6e9eeee1666274e09b5dbfe2895afc728f119da2a8edca0f11de55c42e1e382'
            '4d0be9c20476a000f9bc4ea57eebacd1526ab70f93ebd4fa1d24fdbc51fab251eb8450559ffe628663ad11075577b6de8e678dbdfe30222c1cc4509e4e3c3d2f')

prepare() {
  if [ -d "${pkgbase%-*}-${pkgver}" ]; then
    cd "${pkgbase%-*}-${pkgver}"
  fi
  # Apply Ubuntu's patches

  sed -i -e '/key-certificate-extensions.patch/d' \
         -e '/lp829673_gconf_hide_applet.patch/d' \
         -e '/hide_policy_items_env_var.patch/d' \
         -e '/0001_Move_on_with_enter_in_the_country_page_of_the_new_mobile_wizard.patch/d' \
         -e '/set_atk_relations.patch/d' \
         -e '/11-user-connections.patch/d' \
         ../debian/patches/series

  for i in $(grep -v '#' ../debian/patches/series); do
    msg "Applying ${i} ..."
    patch -p1 -i "../debian/patches/${i}"
  done

  sed -i 's/-Werror//g' m4/compiler_warnings.m4
}

build() {
  if [ -d "${pkgbase%-*}-${pkgver}" ]; then
    cd "${pkgbase%-*}-${pkgver}"
  fi

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --disable-static \
    --disable-more-warnings \
    --disable-maintainer-mode \
    --with-appindicator \
    --with-wwan

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make 
}

check() {
  if [ -d "${pkgbase%-*}-${pkgver}" ]; then
    cd "${pkgbase%-*}-${pkgver}"
  fi
  make -k check
}

package_network-manager-applet-ubuntu() {
  depends=(libnm-gtk-ubuntu libmm-glib libnotify libsecret 
	   'networkmanager>=1.1' 'libnm-glib>=1.1')
  install=network-manager-applet.install
  provides=("network-manager-applet=${pkgver}" nm-connection-editor)
  conflicts=(network-manager-applet)

  if [ -d "${pkgbase%-*}-${pkgver}" ]; then
    cd "${pkgbase%-*}-${pkgver}"
  fi
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" -C src/libnm-gtk uninstall

  # Install Ubuntu stuff
  install -dm755 "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  install -m644 "${srcdir}"/debian/icons/22/nm-device-wired-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  install -m644 "${srcdir}"/debian/icons/22/nm-signal-00-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  ln -snf       nm-signal-00.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-none.png
  ln -snf       nm-signal-00-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-none-secure.png
  install -m644 "${srcdir}"/debian/icons/22/nm-signal-25-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  ln -snf       nm-signal-25.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-low.png
  ln -snf       nm-signal-25-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-low-secure.png
  install -m644 "${srcdir}"/debian/icons/22/nm-signal-50-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  ln -snf       nm-signal-50.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-medium.png
  ln -snf       nm-signal-50-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-medium-secure.png
  install -m644 "${srcdir}"/debian/icons/22/nm-signal-75-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  ln -snf       nm-signal-75.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-high.png
  ln -snf       nm-signal-75-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-high-secure.png
  install -m644 "${srcdir}"/debian/icons/22/nm-signal-100-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  ln -snf       nm-signal-100.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-full.png
  ln -snf       nm-signal-100-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-full-secure.png

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}

package_libnm-gtk-ubuntu() {
  pkgdesc="NetworkManager GUI library"
  depends=(libnm-glib gtk3-ubuntu mobile-broadband-provider-info iso-codes)
  provides=("libnm-gtk=${pkgver}")
  conflicts=(libnm-gtk)

  cd "${pkgbase%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" -C src/libnm-gtk install
}
