# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-session
_actual_ver=12.10.5
_extra_ver=+16.04.20160412
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Indicator showing session management, status and user switching"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-session"
license=(GPL)
depends=(gnome-settings-daemon)
makedepends=(cmake intltool python2)
optdepends=('lightdm: support for lightdm')
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-session_${_actual_ver}${_extra_ver}.orig.tar.gz"
        indicator-session.service
        0001-There-is-no-help.patch
        0002-Remove-url_dispatcher.patch)
sha512sums=('0142ab02e5dba9a543c6785ea1409fe1d95ed57b395f8ceea526fbe4c2b726088d37a927c1fac6c8a3a31125478c7aa76bc45b8687bd96b1125892f84d329529'
            '540c76c0f68b028194bb1000dd61a0c24f2f16427e79d237b94b8c65fe5e3e16eb6ea645f0fd33d734113727e25b6eb7452cb72f72399c9e8d370f74abba7511'
            '01c1ef655db6524aaaeb63abce035c64cbd1c200b15061a18f025c082c020525402aacacc1059e70ed84e206fd1e339aa5ebf97e5c168b123d868821063a9b85'
            '191b41d7a0a82c909f0653bcaa4d6dead2e661e62dbdaa6ed6690180a12451d7f791c95bf0c6ad64f284498b5329ac21b54cad500734f5f9dfa4d3796b44a0b5')

prepare() {
  cd .

  patch -p1 -i ../0001-There-is-no-help.patch
  patch -p1 -i ../0002-Remove-url_dispatcher.patch
}

build() {
  cd .

  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -Denable_tests=OFF \
    -Denable_lcov=OFF

  make
}

package() {
  cd ./build

  make DESTDIR="${pkgdir}/" install

  rm -vf "${pkgdir}/test-service"

  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m644 "${srcdir}/indicator-session.service" \
                "${pkgdir}/usr/share/dbus-1/services/"

  rm -r "${pkgdir}/usr/share/upstart/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}
