# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Contributor: ME <enriquezmark36@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=gtk+3.0, repo=yakkety
# vercheck-archlinux: name=gtk3, repo=extra, arch=x86_64
# vercheck-gnome: name=gtk+, majorver=3.18
# vercheck-ppa: name=gtk+3.0, url=ppa:gnome3-team/gnome3-staging

_use_ppa=false

pkgname=gtk3-ubuntu
_ppa_rel=0ubuntu1~yakkety1
_ubuntu_rel=1ubuntu1
_ubuntu_ver=3.20.6
#pkgver=3.18.9
pkgver=3.20.6
pkgrel=4
pkgdesc="GObject-based multi-platform toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libcups libepoxy libxcursor libxinerama
         libxrandr libxi libxcomposite libxdamage pango shared-mime-info
         colord at-spi2-atk wayland libxkbcommon adwaita-icon-theme
         json-glib rest wayland-protocols)
makedepends=(gobject-introspection)
provides=("gtk3=${pkgver}")
conflicts=(gtk3)
license=(LGPL)
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz"
	unity-headerbar-maximized-mode.patch
	unity-border-radius.patch)
sha512sums=('597d25ec00f83c6ac77784f303458fad03d8ad017a53ee59e067fda61fa79fe6f34e46391f45844df7c4bbb2c8fa032b32f6d1c8f2f324c99681069f4d6d9be4'            'eafb65795074e1d8b723e576f12f94eb143f5a62318ced11136edb8e345776fb87d3418ea1cb1895106214d5713d28387dbf3f3e63b4dda8baa1960b278ab441'
'565d6f714905d130ab18151602359ee3d995f926f4434b3bb8b99d7c358e612d9f0bfbb2acf1dc3b68b718764e8f33865f3f2f7f978d7fd3a8ef49a828a0f2d8')
if [[ "${_use_ppa}" != "true" ]]; then
  source+=("https://launchpad.net/ubuntu/+archive/primary/+files/gtk+3.0_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
  sha512sums+=('f178621894f58b33e06c392f163dadc9c08a8a0f6559009101b854187e7133032d5ee96ed64363fe19744a37d182a628051d8c67a884f44581e9d30310792b22')
else 
  source+=("http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gtk+3.0/gtk+3.0_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz")
  sha512sums+=('858ae4cca4382aad2031d2cf25f89f41f7258f16a98078c1a74f99d0d9600b6eaa51bebfc46b76bbdf5ca08508e8b4aa3653b194c556f59f3f04f1ae819e3036')
fi

prepare() {
    cd "gtk+-${pkgver}"

    # Apply Ubuntu Patches
    echo > ../debian/patches/series

    # https://bugs.launchpad.net/indicator-messages/+bug/1088162
    # Dependency check: grep -R ubuntu-private.h * | cut -d/ -f1
    echo 'ubuntu_gtk_custom_menu_items.patch' >> ../debian/patches/series
    # Do not allow offscreen widgets to grab the cursor
    echo '016_no_offscreen_widgets_grabbing.patch' >> ../debian/patches/series
    echo '017_no_offscreen_device_grabbing.patch' >> ../debian/patches/series
    # Allow printing to printers advertised using Avahi/Bonjour when CUPS 1.6
    echo 'print-dialog-show-options-of-remote-dnssd-printers.patch' >> ../debian/patches/series
    # Unity-specific
    #echo '0001-Add-style-classes-to-the-title-buttons.patch' >> ../debian/patches/series
    if [[ "${_use_ppa}" != "false" ]]; then
    echo 'message-dialog-restore-traditional-look-on-unity.patch' >> ../debian/patches/series
    patch -p1  -i ${srcdir}/unity-border-radius.patch
    patch -p1 -i ${srcdir}/unity-headerbar-maximized-mode.patch
    fi	
    # Other
    echo 'bzg_gtkcellrenderer_grabbing_modifier.patch' >> ../debian/patches/series
    for i in $(grep -v '#' ../debian/patches/series); do
        msg "Applying ${i} ..."
        patch -p1 -i "../debian/patches/${i}"
    done
}

build() {
    cd "gtk+-${pkgver}"

    autoreconf -vfi

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-schemas-compile \
        --enable-x11-backend \
        --enable-broadway-backend \
        --enable-wayland-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make -j4
}

package() {
    cd "gtk+-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "$pkgdir/usr/bin/gtk-update-icon-cache"
}
