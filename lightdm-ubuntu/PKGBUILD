# Maintainer: Michael Healy <horsemanoffaith@gmail.com>

# This package no longer depends on a specific toolkit. It will build the GTK and
# Qt libraries and recommend the installation of those respective tookits using
# optdepends.

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=yakkety
# vercheck-launchpad: name=${pkgname%-*}

pkgname=lightdm-ubuntu
_ubuntu_rel=0ubuntu1
pkgver=1.19.3
pkgrel=1
pkgdesc="A lightweight display manager"
arch=(i686 x86_64)
url="https://launchpad.net/lightdm"
# Libraries are LGPLv3+, everything else is GPLv3+
license=(GPL3 LGPL3)
groups=(unity)
depends=(libgcrypt libxcb libxdmcp libxklavier)
# Useful dependencies, but not required
#depends=('gnome-themes-standard' 'gnome-backgrounds' 'gnome-icon-theme' 'webkitgtk3')
makedepends=(gnome-common gobject-introspection gtk-doc gtk3-ubuntu intltool qt4 qt5-base yelp-tools)
optdepends=('accountsservice: DBus interface for querying user information'
            'gnome-keyring: For pam_gnome_keyring.so in the greeter PAM config'
            'gtk3: For using the GTK greeter'
            'lightdm-unity-greeter: Default Ubuntu 16.04 Greeter'
            'qt4: To use the qt4 version of liblightdm-qt'
            'qt5-base: To use the qt5 version of liblightdm-qt'
            'xorg-server: Needed to run X')
provides=("lightdm=${pkgver}" "liblightdm-qt4=${pkgver}")
conflicts=(lightdm liblightdm-qt4)
options=(emptydirs)
backup=(etc/lightdm/keys.conf
        etc/lightdm/lightdm.conf
        etc/lightdm/users.conf)
install=lightdm.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/lightdm_${pkgver}-${_ubuntu_rel}.tar.gz"
        lightdm.tmpfiles.d
        lightdm.rules
	lightdm.service
        0001-guest-account-Use-cross-distro-commands.patch)
sha512sums=('4c43309a5f0520047108646dcc8c5ede6da5f1478fd2ed7df30da738efe36129b1c98ab1872391a04b64ccf38afc3d28f99137a92fa824b3d0e60838490e2c97'
            '81a76a49eb208b1d33f5ac0184d87a377cd37c522d74a93ccd3d96b3d6e32c44872a65873b91fec3daba0846cdb5a938b51944697e636c045d03259bd5424644'
            '8d6aa12c4d129c25e56ecf2904db4e294d46631d11bd8bec2f20a76c871ba758094abb24616d3d2038a684fbb736ee61d1f80697d525d62c4dc68113e101194f'
            '4b845c4286be743f47431729ee99874f2042f9a17740b59641c5d065c90cc12b232a3b6cd057a20ee1c338ec6320168b5de6ee616f2b72f0fe7a84a11e49ebd1'
            '67c32e5d0865efd6d8a8bad42efa52eaa36ad4ec5c31e083edb908a5ef5ff489905ca34a46714d83e2875dcc5c96606d63d1c42fad3e555b48cbc6bd9f559ead')
            
prepare() {
  cd "lightdm-${pkgver}"

  #patch -p1 -i "../lightdm_${pkgver}-${_ubuntu_rel}.diff"

  # Apply Ubuntu patches

  # Disable patches
    # Do not use Ubuntu's language-tools
      sed -i '/04_language_handling.patch/d' debian/patches/series

  for i in $(grep -v '#' debian/patches/series); do
    patch -p1 -i "debian/patches/${i}"
  done

   # Do not depend on Debian/Ubuntu specific adduser package
  patch -p1 -i ../0001-guest-account-Use-cross-distro-commands.patch
}

build() {
  cd "lightdm-${pkgver}"

  export MOC4=moc-qt4
  export MOC5=moc-qt5

  gtkdocize
  aclocal --install --force
  autoreconf -vfi
  intltoolize -f

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static \
    --libexecdir=/usr/lib/lightdm \
    --localstatedir=/var \
    --with-user-session=ubuntu \
    --with-greeter-user=lightdm \
    --disable-libaudit

  make
}

package() {
  cd "lightdm-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/lib/systemd/system/"
  install -m755 "${srcdir}"/lightdm.service \
		"${pkgdir}/usr/lib/systemd/system/"


  # Additional LightDM configuration files
  install -dm755 "${pkgdir}"/etc/lightdm/lightdm.conf.d/
  install -m644 debian/50-{xserver-command,greeter-wrapper,guest-wrapper}.conf \
                "${pkgdir}"/etc/lightdm/lightdm.conf.d/

  # Install configuration files
  install -dm755 "${pkgdir}"/usr/share/doc/lightdm/
  install -m644 "${pkgdir}"/etc/lightdm/lightdm.conf \
                "${pkgdir}"/usr/share/doc/lightdm/
  install -m644 "${pkgdir}"/etc/lightdm/keys.conf \
                "${pkgdir}"/usr/share/doc/lightdm/

  # Install binaries and scripts
  install -dm755 "${pkgdir}"/usr/bin/
  install -m755 debian/guest-account.sh "${pkgdir}"/usr/bin/guest-account
  install -m755 debian/lightdm-session "${pkgdir}"/usr/bin/
  install -m755 debian/lightdm-greeter-session "${pkgdir}"/usr/lib/lightdm/
  install -m755 debian/config-error-dialog.sh "${pkgdir}"/usr/lib/lightdm/

  # Install systemd tmpfiles.d file
  install -dm755 "${pkgdir}"/usr/lib/tmpfiles.d/
  install -m644 "${srcdir}"/lightdm.tmpfiles.d \
                "${pkgdir}"/usr/lib/tmpfiles.d/lightdm.conf

  # Install PolicyKit rules from Fedora which allow the lightdm user to access
  # the systemd-logind, consolekit, and upower DBus interfaces
  install -dm750 "${pkgdir}"/usr/share/polkit-1/rules.d/
  install -m644 "${srcdir}"/lightdm.rules \
                "${pkgdir}"/usr/share/polkit-1/rules.d/

  # Configuration settings that differ from Ubuntu
  sed -i \
    -e 's/^\(minimum-uid=\).*$/\11000/g' \
    -e 's@/usr\(/sbin/nologin\)$@\1@g' \
    "${pkgdir}"/etc/lightdm/users.conf

  # Configuration files specific to Ubuntu
  rm -rvf "${pkgdir}"/etc/init/

  # Create GSettings defaults directory
  install -dm755 "${pkgdir}"/etc/guest-session/gsettings/

  # Remove apparmor stuff
  rm -rvf "${pkgdir}"/etc/apparmor.d/
}
