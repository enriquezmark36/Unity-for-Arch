# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-datetime
_actual_ver=13.10.0
_extra_ver=+15.10.20151005
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A very, very simple clock"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-datetime"
license=(GPL)
depends=(evolution-data-server gstreamer libtimezonemap properties-cpp
         unity-control-center)
makedepends=(intltool cmake)
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Get-timezone-from-systemd-s-timedate1-interface.patch
        0002-Not-a-phone.patch
        0003-Add-support-for-EDS-3.16-API.patch)
sha512sums=('3d28cab736f427c3902460a76b93ef8f7ca5ad3738b909829e4b25a4fb8b356c8ab230d3bb7d0a53c727cd324e681feaa2eb72107415b11d46a402d0bf6672e4'
            '0bf981faa8a7dea78f9d4c5c6c1a540cc3f6fba087849d834c18c807bb1f39bc82891142e66d32646cd807d140ff6f75ff13b0394dd6d7480441754833f54e57'
            'f7a38476245a233454463ccef33af52dc62738640161de8647185ad26f1b1e3004b10e4adb3fa841b58143f502330776868e116657579be6bd3e076fed184759'
            'f1597b7b46390d786d3e2c04477ae612987508f22fada666402cf514b2b9bc99ced7f8c3b34b70fa91422ed05ac139e8d05b48c0c148f57c39ba323958aef21f')
prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  # Port to systemd's timedated (because /etc/timezone is no longer used)
  patch -p1 -i ../0001-Get-timezone-from-systemd-s-timedate1-interface.patch
  patch -p1 -i ../0002-Not-a-phone.patch
  patch -p1 -i ../0003-Add-support-for-EDS-3.16-API.patch
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS+=" -Wno-error"

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -Denable_tests=OFF

  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}/build/"

  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/share/upstart/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}
