# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-datetime
_actual_ver=15.10
_extra_ver=+16.10.20160629
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A very, very simple clock"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-datetime"
license=(GPL)
depends=(evolution-data-server gstreamer libtimezonemap properties-cpp
         unity-control-center libaccounts-glib)
makedepends=(intltool cmake)
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Get-timezone-from-systemd-s-timedate1-interface.patch
        0002-Not-a-phone.patch
	0003-remove-ubuntu-app-launch-2.patch)
sha512sums=('9446805977324ac8fbaa9901107b60e76621f3bc64f5493f71c2bcf3f0bcc07f705ec71425ff2ddc793561cfb5ff9577338990477175501606cd527ce9d70595'
            'a7f956ef0d6ba3282941d14723845dc982cdcb95504b717bbbf8cf32582949d209ff4fc8ee3a05069f7c396b76ef113ffb449ceb7eaa69f73969b3f6b32b90e0'
            '2bb1ffdcff1a8932ae1b4ea76d2eac02f68193c3437e31d04ba64fd8641400a6cce15aa72874eda86f14a224a1a95a79ae23d55d77597991404e2169e4e34733'
            'be586fd34e03b3a191fbeea1912056595542ca91fa4636819ae718e57db3281fc0df268b08c25ec727b6357d4645035bf39a18690c755670f0386ed487b247e7')

prepare() {
  cd .

  # Port to systemd's timedated (because /etc/timezone is no longer used)
  patch -p1 -i ../0001-Get-timezone-from-systemd-s-timedate1-interface.patch
  patch -p1 -i ../0002-Not-a-phone.patch
  patch -p1 -i ../0003-remove-ubuntu-app-launch-2.patch
#  patch -p1 -i ../0003-Add-support-for-EDS-3.16-API.patch
}

build() {
  cd .

  export CFLAGS+=" -Wno-error"

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -Denable_tests=OFF

  make
}

package() {
  cd ./build

  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/share/upstart/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}
