# Maintainer: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-power
_actual_ver=12.10.6
_extra_ver=+16.10.20160708
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="An indicator to show the power status of your devices"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-power"
license=(GPL)
depends=(dconf libnotify upower unity-control-center)
makedepends=(cmake intltool python2)
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-power_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Not-a-phone.patch
        0002-Remove-accountsservice-dependency.patch)
sha512sums=('639e62fdfe1b6712a107be184c789d4ba218086699bb9ffcc414d9a64bee82304455ee8421f5351bb0dcc6c022bbddf8c8183b0696b522a6a65eb00bc1838cb9'
            'b2c5e180dbbc3695d9c6ff3d684b9da61ed9963c8d8d2c2b7224b6574137cf147a81f492e4fb92c25bf1d546e8062cd6b9db8005cf606830222e32cf6800d2d5'
            '18a9dade1df6fd622099d402a40662edd27354f644673870806ea4137440869794ae46b605330d1b1785707dca120560cb2562d40be7c22b3976206aff8c5204')

prepare() {
  if [ -d "${pkgname}-${_actual_ver}${_extra_ver}" ]; then
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
  fi

  patch -p1 -i ../0001-Not-a-phone.patch
  patch -p1 -i ../0002-Remove-accountsservice-dependency.patch

  sed -ie '/NotShowIn=/d' data/indicator-power.desktop.in
}

build() {
  if [ -d "${pkgname}-${_actual_ver}${_extra_ver}" ]; then
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
  fi
  export CFLAGS+=" -Wno-error"

  rm -rf build; mkdir build; cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/ \
      -Denable_tests=OFF
  make
}

package() {
  if [ -d "${pkgname}-${_actual_ver}${_extra_ver}" ]; then
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
  fi
  
  cd build 
  
  make DESTDIR="${pkgdir}/" install

  find "${pkgdir}" -type f -name '*.a' -delete

  rm -r "${pkgdir}/usr/share/upstart/"
}
