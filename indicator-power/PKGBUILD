# Maintainer: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-power
_actual_ver=12.10.6
_extra_ver=+16.04.20160105
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="An indicator to show the power status of your devices"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-power"
license=(GPL)
depends=(dconf libnotify upower unity-control-center)
makedepends=(cmake intltool python2)
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-power_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Not-a-phone.patch
        0002-Remove-accountsservice-dependency.patch)
sha512sums=('90f6b08d3bc4f86ad429f8dfa6c1cf5a250ab1747d5cab83158b9540810304bf5666d79f51c99f35a2039fcaf8f71b7743b644001a10258174403639db77fd8b'
            'd51f3abe6c32ed5f9009e3633ed070f3985980e279504091c6ee8b942bca99c414fb465c8e6bacb333d2fc3412de673ed4a65f6f658003a51fdf36eee43a0431'
            'f71b333a8532af4bc2d71d18097f2b05093bcf68c6a15b9da930dba03e4b342c98afc6de910a7b99d2640b5cca589e31038b5240c1718aba274da8556faa52e0')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001-Not-a-phone.patch
  patch -p1 -i ../0002-Remove-accountsservice-dependency.patch

  sed -ie '/NotShowIn=/d' data/indicator-power.desktop.in
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS+=" -Wno-error"

  rm -rf build; mkdir build; cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/${pkgname} \
      -Denable_tests=OFF
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}/build/"

  make DESTDIR="${pkgdir}/" install

  find "${pkgdir}" -type f -name '*.a' -delete

  rm -r "${pkgdir}/usr/share/upstart/"
}
