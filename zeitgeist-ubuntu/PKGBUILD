# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=wily
# vercheck-launchpad: name=${pkgname%-*}

pkgname=zeitgeist-ubuntu
pkgver=0.9.16
_ubuntu_rel=0ubuntu3~gcc5.1
pkgrel=1
pkgdesc="Service logging user activities and events"
arch=(i686 x86_64)
url="http://zeitgeist-project.com/"
license=(GPL2 LGPL2.1)
depends=(json-glib telepathy-glib gtk3 xapian-core)
makedepends=(intltool gobject-introspection vala raptor python2-rdflib)
provides=(zeitgeist-datahub "zeitgeist=${pkgver}")
conflicts=(zeitgeist-datahub zeitgeist)
replaces=(zeitgeist-datahub)
source=("https://launchpad.net/zeitgeist/${pkgver%.*}/${pkgver}/+download/zeitgeist-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/zeitgeist_${pkgver}-${_ubuntu_rel}.debian.tar.xz")
sha512sums=('eb28215c937b71c8cec12f55236db4281ad529b2854ecb3c093477d45cd8348c6ea764e2cf00a99d41645b27b685a5325f0631b318409735b578a5871087b0f1'
            '291d126252e6514789507ff0a3b18ec9dad10ffdbba9772022650250bcba750c6bed4cb1061a2c0e43cda70fb336ce7d2155b3b598b8baa0d5e73b0b67a3d547')

prepare() {
  cd "zeitgeist-${pkgver}"

  sed -i 's/python -/$PYTHON -/' configure configure.ac

  for i in $(grep -v '#' ../debian/patches/series); do
    msg "Applying ${i}"
    patch -p1 -i "../debian/patches/${i}"
  done
}

build() {
  cd "zeitgeist-${pkgver}"
  export PYTHON=/usr/bin/python2
  autoreconf -vfi
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/zeitgeist \
    --enable-datahub \
    --enable-fts \
    --enable-telepathy
  make
}

package() {
  cd "zeitgeist-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
