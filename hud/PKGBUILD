# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=wily
# vercheck-launchpad: name=${pkgname}

pkgname=hud
_actual_ver=14.10
_extra_ver=+15.10.20151012
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=3
pkgdesc="Backend for the Unity HUD"
arch=(i686 x86_64)
url="https://launchpad.net/hud"
license=(GPL)
groups=(unity)
depends=(bamf-ubuntu libdbusmenu-gtk3 sqlite gsettings-qt libcolumbus
         libdbusmenu-qt5 dee-qt)
makedepends=(cmake gnome-common gnome-doc-utils gobject-introspection gtk-doc
             intltool qt5-base python2-distribute vala)
optdepends=('indicator-appmenu: HUD support')
install=hud.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/hud_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001_Python_2.patch
        0002_Remove_tests.patch
        0003_Remove_voice_recognition.patch)
sha512sums=('eedc155fd3914e075994caef6c39efe4f5738fef8679653c838248226add20b27f68395c556df6441aec0cf667d0f75065c7066334b1f67d1c42faa720b2f4e5'
            '1d560db046c56ecff0d6566c98684962f179326009fb2bfdbaab3fc1df265afa5cb2da202fb13b4889ade0244eaf39f3aea4d7c9e434c2bd1334f75bc969cc90'
            'c38e555315f16014aa66c20dde02d8902db97de44b9771d0d4269758889cf4a3106da26300d2a26177fd36b66847c9cd2f2117355e40239b2fa0a8c9238594eb'
            '66ad6b36cff47e3fe9c75d728e8297dbcee4122bf47858c231b8425ca3925afd31a23715d4f63b98abc974e06a354fe526ac6d20e8b57bdb9a6f23e8045d9753')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001_Python_2.patch
  patch -p1 -i ../0002_Remove_tests.patch
  patch -p1 -i ../0003_Remove_voice_recognition.patch

  sed -i '/mscgen/d;/UseMscgen/d' docs/CMakeLists.txt
  rm cmake/UseMscgen.cmake
  rm cmake/FindGMock.cmake

  # Window-stack-bridge service must be running for hud-service to return search results #
  sed -e "/@pkglibexecdir@\/hud-service/i \
          trap 'kill $\(jobs -pr\)' SIGINT SIGTERM EXIT\n \
          @pkglibexecdir@\/window-stack-bridge &" \
          -i data/dbus-activation-hack.sh.in

  # Work around build issues (gtk-doc will no longer be built)
  rm cmake/GtkDocScanGObjWrapper.cmake
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  export CXXFLAGS+=" -lc"

  rm -rf build && mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -DENABLE_DOCUMENTATION=ON \
    -DENABLE_TESTS=OFF \
    -DENABLE_VOICE_TESTS=OFF \
    -DENABLE_SCALABILITY_TESTS=OFF \
    -DFULL_WARNINGS=ON

  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}/build"
  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/share/upstart/"
}
