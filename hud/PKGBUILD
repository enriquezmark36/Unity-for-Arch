# Maintainer: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenail
# vercheck-launchpad: name=${pkgname}

pkgname=hud
_actual_ver=14.10
_extra_ver=+16.04.20160415
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Backend for the Unity HUD"
arch=(i686 x86_64)
url="https://launchpad.net/hud"
license=(GPL)
groups=(unity)
depends=(bamf-ubuntu libdbusmenu-gtk3-ubuntu sqlite gsettings-qt libcolumbus
         libdbusmenu-qt5 libqtdee)
makedepends=(cmake gnome-common gnome-doc-utils gobject-introspection gtk-doc
             intltool qt5-base python2-distribute vala)
optdepends=('indicator-appmenu: HUD support')
install=hud.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/hud_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001_Python_2.patch
        0002_Remove_tests.patch
        0003_Remove_voice_recognition.patch)
sha512sums=('84cb695dcaa27be2cf30fdf34eb8bb17089729ce18a663287fc48af0207ba1cf680cbf5df20a4d028e4c3c42fc05fe370be3ab83a3bb3d11dfbe631a6923a9d0'
            '1d560db046c56ecff0d6566c98684962f179326009fb2bfdbaab3fc1df265afa5cb2da202fb13b4889ade0244eaf39f3aea4d7c9e434c2bd1334f75bc969cc90'
            'f67ec837c9cf50a3bfbbb40bfa53545d484d966ef09f2c39cce757235e3f310e7339dc9e83da095b18f0cb3aca02a978aba06ccf9e1572e84eb6521b0fdcf422'
            '66ad6b36cff47e3fe9c75d728e8297dbcee4122bf47858c231b8425ca3925afd31a23715d4f63b98abc974e06a354fe526ac6d20e8b57bdb9a6f23e8045d9753')

prepare() {
  cd .

  patch -p1 -i ../0001_Python_2.patch
  patch -p1 -i ../0002_Remove_tests.patch
  patch -p1 -i ../0003_Remove_voice_recognition.patch

  sed -i '/mscgen/d;/UseMscgen/d' docs/CMakeLists.txt
  rm cmake/UseMscgen.cmake
  rm cmake/FindGMock.cmake

  # Window-stack-bridge service must be running for hud-service to return search results #
  sed -e "/@pkglibexecdir@\/hud-service/i \
          trap 'kill $\(jobs -pr\)' SIGINT SIGTERM EXIT\n \
          @pkglibexecdir@\/window-stack-bridge &" \
          -i data/dbus-activation-hack.sh.in

  # Work around build issues (gtk-doc will no longer be built)
  rm cmake/GtkDocScanGObjWrapper.cmake
}

build() {
  cd .

  export CXXFLAGS+=" -lc"

  rm -rf build && mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -DENABLE_DOCUMENTATION=ON \
    -DENABLE_TESTS=OFF \
    -DENABLE_VOICE_TESTS=OFF \
    -DENABLE_SCALABILITY_TESTS=OFF \
    -DFULL_WARNINGS=ON

  make
}

package() {
  cd ./build
  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/share/upstart/"
}
