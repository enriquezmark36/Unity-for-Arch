# Maintainer: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=yakkety
# vercheck-archlinux: name=${pkgname%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgname%-*}, majorver=3.19

pkgname=gsettings-desktop-schemas-ubuntu
_ubuntu_ver=3.18.1
_ubuntu_rel=1ubuntu1
_extra_ver=0ubuntu1~xenial1
pkgver=3.20.0
pkgrel=1
pkgdesc="Shared GSettings schemas for the desktop"
arch=(any)
url="http://live.gnome.org/"
license=(GPL)
depends=(glib2)
makedepends=(intltool gobject-introspection)
provides=("${pkgname%-*}=${pkgver}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
	"https://launchpad.net/~gnome3-team/+archive/ubuntu/gnome3-staging/+files/${pkgname%-*}_${pkgver}-${_extra_ver}.debian.tar.xz"
)
sha512sums=('559baf477be9f728c5fea24232218ec4ff17ac97a6db166749fbca4dc1d3995e79824dc977cae5aa10b2197aa9c0e65b884f5c1cb446db109bc15dafe456f7a1'
            'b031d5754508e47f90b737d5941abe9adbe1d0f5b0bd4b0deae6a7c39d9b2f874de372801ceb019a98455496d58a5b8beacb3e5c94b44199a040af63867b9551')
prepare() {
  cd "${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches
   #But not this...
  sed -i '/git_revert_notifications_key_removal.patch/d' ../debian/patches/series

  for i in $(grep -v '#' ../debian/patches/series); do
    msg "Applying ${i} ..."
    patch -p1 -i "../debian/patches/${i}"
  done

}

build() {
  cd "${pkgname%-*}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-compile
  make
}

package() {
  cd "${pkgname%-*}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
