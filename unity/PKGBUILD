# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Contributor: ME (enriquezmark36@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=unity
_actual_ver=7.5.0
_extra_ver=+16.10.20160715
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A desktop experience designed for efficiency of space and interaction"
arch=(i686 x86_64)
url="https://launchpad.net/unity"
license=(GPL)
depends=(bamf-ubuntu boost cairo compiz-ubuntu clutter-gtk gjs gnome-desktop
         gnome-screensaver gnome-session-ubuntu hud ido-ubuntu libgnomeui libindicator-gtk2-ubuntu
         libindicator-gtk3-ubuntu libnotify libunique libunity libunity-misc libxfixes nux
         unity-asset-pool unity-scope-home libxi zeitgeist-ubuntu gsettings-ubuntu-schemas
         lightdm-unity-greeter gmock gtest chrpath appstream-glib)
groups=(unity)

#options=(!ccache)
install=unity.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        0001-Disable-tests.patch
        0002-Revert-r3134-Remove-Systray-Whitelist.patch
        0003-Disable-Werror.patch
        0004-Fix-launchers.patch
        0005-Remove-Upstart-stuff.patch
        0006-Remove-social.scope.patch
        0007-Remove-xpathselect-dependency.patch
        launcher_bfb.png
        99unity-panel-service
        unity-debug
        run_this_if_unity_is_on_fire.py
        unity.pam
	unity.desktop
	wist_02_window_buttons.patch)
sha512sums=('aa22adb23cebd2b64f1d1f5b04cf4090d9a20aa769a66cc26c0725798674448ac3d90a30165f587d4d27df4f0d6682f962ee18f029d67ba3ed213a5d5094acdd'
            '825526aee940d53337aba40b3261d4e64594eefd7b2450798ce8c9a153ac257b27dec588b3a33b5297a3669a9e35883c5f80ef84533c3cbd052bf378cb13708e'
            '34a7878f6823b913a9eb12e799cfe73fc24afec74069fdec2e2245df273a99ef8c365c56dc7a34853d9aa30c49abd99cd76da7c21ecad863cf5a6ca02b91d340'
            'b5e75fb4580195cd997af95aad65a21404477a3233546458639f7ea71cd5c4295d9886adc444179866fc5dac9cf1a98e2c56fe7f48aafbab00c6c466a3c99a23'
            '3b9c0181426356c635be8771a4308e5783165201a446271109990c2f2db2eb586ebb26efc32606360658b552128b28c2f8469a26da58a075cc9b50c36cd5124e'
            '8ad5940a78a839cf50776414c0fa7c4c9b0ffa0b0e3051705da4a090a5808f31048efbbdf181fccb7966a2d472dab6e8b809150fed52f461345837a42d85365c'
            'a0751f706370c667c8b6557887ab7ae84bc9749aac34d8654dad3f517f94d45135037ef8b7047167baf2d563f95b1d87876e4f3a1fe498b4c216042920c91eb6'
            'ed2d7c211fa8fda79798e6497eed1763e2a39427d1352df9c93fe1e5eaf30c1db0aaa90012bc52fd5f76823bc0e382cf7d626f8234a632c32ba7ac5ed53ca5a7'
            '0f101f9912e0684733e1d0b3317a9c3b6b39167af1ecf1ba9610f8ecedf14461cfb94a4ef6149def54f92a9ce8b790f44507ee48675e2cef2e8688973b77a610'
            'ec9ea91d79129b23aae6c4b9584fb396ecc572a0bafcac6229cc413ee441f610cb51ffce9383544c9fc62e747d5718be9be050850943eac4820095f190dc0ed0'
            '126bdb4a4515e029acb31c255299caee06b246800f7fc833f0bd80a5d90e95b5a38000620cbba627a607100e6f734c4b1b677c84407c7d87cc8561aef102758e'
            'b217490bf5eb5b0e83dbc9737b80b2ab9d7288362f370625f3c99d5a862c0e1947376b9fab4fdc9c2ddd4e8bd6e8d2ff5763a3a4391732f0781abbc60b159db5'
            'f76ce70ee8ad4a83c94a5919fcabadf7f82e90cf3c70079344e8ab4069dd41f9055f86e01ee5d96ff52819ed9979bdbe85c59e0d50aa4c511e5a20e19a9ea06d'
            '1067bcb25b6d6d01256b176b5854d1ace700ba2b7323b4af257aa95d2f47d5043ac22811f65e99f1e961772cd1e81c153ef69b162918827bd9d7d50d458908df'
            '0ca3f7d57ee8ca40b867c2439a75956a7b64359c688eca28c64115ea4282f34ef496b121f9e53c11cf5c13dbbe9c71217a4e9e846247b254721fb3f4effca31a'
            '63ebf209ba6c42df01afea7e69d047ec4cca03525076795efcec324e37e85c5b468ec4143388d65a134edc22adf7f842ef3c0566b90f6b3560e4b4b90ac6b346')

prepare() {
  if [ -d ${pkgname}-${_actual_ver}${_extra_ver} ]; then
     cd "${pkgname}-${_actual_ver}${_extra_ver}"
  fi

  patch -p1 -i ../0001-Disable-tests.patch
  patch -p1 -i ../0002-Revert-r3134-Remove-Systray-Whitelist.patch
  patch -p1 -i ../0003-Disable-Werror.patch
  patch -p1 -i ../0004-Fix-launchers.patch
  patch -p1 -i ../0005-Remove-Upstart-stuff.patch
  patch -p1 -i ../0006-Remove-social.scope.patch
  patch -p1 -i ../0007-Remove-xpathselect-dependency.patch
  #For those who want those lefty-buttons to be right for once
  #patch -p1 -i ../wist_02_window_buttons.patch
  
  # Apply Ubuntu patches
  patch -p1 -i "./unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
  for i in $(grep -v '#' ./debian/patches/series); do
    patch -p1 -i "./debian/patches/${i}"
  done
}

build() {
 if [ -d ${pkgname}-${_actual_ver}${_extra_ver} ]; then
     cd "${pkgname}-${_actual_ver}${_extra_ver}"
  fi
  export CXXFLAGS+=" -lc"

  # (From debian/rules) http://ccache.samba.org/manual.html#_precompiled_headers
  export CCACHE_SLOPPINESS=time_macros

#  [[ -d build ]] && rm -rvf build/
  mkdir -p build/ && cd build/

  cmake .. -Wno-dev \
    -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
    -DCOMPIZ_PACKAGING_ENABLED=TRUE \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SYSCONFDIR=/etc \
    -DUSE_GSETTINGS=TRUE \
    -DBUILD_XORG_GTEST=OFF \
    -DCOMPIZ_BUILD_TESTING=OFF \
    -DENABLE_UNIT_TESTS=OFF 

  make

  # Make sure that the GSettings schema files were created
  gen=generated/glib-2.0/schemas
  if \
    [ ! -f ${gen}/org.compiz.networkarearegion.gschema.xml ] || \
    [ ! -f ${gen}/org.compiz.unitydialog.gschema.xml ] || \
    [ ! -f ${gen}/org.compiz.unitymtgrabhandles.gschema.xml ] ||
    [ ! -f ${gen}/org.compiz.unityshell.gschema.xml ]; then
    make compiz_gsettings_compile_local
  fi
}

package() {
  if [ -d ${pkgname}-${_actual_ver}${_extra_ver} ]; then
     cd "${pkgname}-${_actual_ver}${_extra_ver}"
  fi
  cd build
  make DESTDIR="${pkgdir}/" install
  # Install profile convert files
  install -dm755 "${pkgdir}/usr/lib/compiz/migration/"
  install -m644 ../tools/convert-files/* "${pkgdir}/usr/lib/compiz/migration/"

  # Taken from Ubuntu source package's debian/rules file
  find "${pkgdir}/usr/lib" -name \*.*a -exec rm {} \;
  rm -vf "${pkgdir}/usr/share/compiz/networkarearegion.xml"
  rm -vf "${pkgdir}/usr/lib/compiz/libnetworkarearegion.so"
  rm -vf "${pkgdir}/usr/share/compiz/unitydialog.xml"
  rm -vf "${pkgdir}/usr/lib/compiz/libunitydialog.so"

  # Fix Python 2 scripts
#  sed -i 's|^\(#!.*python$\)|\12|g' \
#    "${pkgdir}/usr/bin/unity" \
#    "${pkgdir}/usr/lib/unity/makebootchart.py"

  # Arch Linux logo
  install -m644 "${srcdir}/launcher_bfb.png" \
    "${pkgdir}/usr/share/unity/icons/launcher_bfb.png"

  # Fix launching of unity-panel-service (from unity-gentoo project)
  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m755 "${srcdir}/99unity-panel-service" \
                "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  # Install script for running Unity with debug output (from unity-gentoo project)
  install -m755 "${srcdir}/unity-debug" "${pkgdir}/usr/bin/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
 
  # Fix insecure rpath in libunityshell.so
  if [ -f "$pkgdir/usr/lib/compiz/libunityshell.so" ]; then
     chrpath --replace /usr/lib/libunity:/usr/lib/compiz "$pkgdir/usr/lib/compiz/libunityshell.so"
  else
     chrpath --replace /usr/lib/libunity:/usr/lib/compiz "$pkgdir/usr/lib64/compiz/libunityshell.so"
  fi

  # FIRE!
  install -m755 "${srcdir}/run_this_if_unity_is_on_fire.py" "${pkgdir}/usr/bin/"

  #Create /etc/pam.d even if it exists
  mkdir -p ${pkgdir}/etc/pam.d

 # Auto-Start!!!
  mkdir -p ${pkgdir}/etc/xdg/autostart/
  install -m755 "${srcdir}/unity.desktop" "${pkgdir}/etc/xdg/autostart/"
  chmod +x "${pkgdir}/etc/xdg/autostart/unity.desktop"

  # Install PAM configuration file
  rm -f "${pkgdir}/etc/pam.d/unity"
  install -m644 "${srcdir}/unity.pam" "${pkgdir}/etc/pam.d/unity"
}
