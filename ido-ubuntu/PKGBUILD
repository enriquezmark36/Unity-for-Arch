# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Contributor: Balló György <ballogyor+arch@gmail.com>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=ido-ubuntu
_actual_ver=13.10.0
_extra_ver=+15.10.20151002
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Widgets and other objects used for indicators"
arch=(i686 x86_64)
url="https://launchpad.net/ido"
license=(LGPL)
depends=(gtk3-ubuntu)
makedepends=(gobject-introspection gtk-doc vala xorg-util-macros)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/ido_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Temporarily-disable-previous-button-drop-shadow-draw.patch)
sha512sums=('df6b991819da8c680b8019c4834bfe35ca2de8c22c4a49e8f2775da47ebd705df91b73bf95b98d0e9cd3cdad1244c75996febdf7c3a89f3eea258c12411ae398'
            '54499681f78a2aa6bcee828fa3352d7901bdb127a27f9621c42f769b338cfbdb6b32094d7ee42380180dfec6eefb4896315830122736519bb61f9873f5ba1073')

prepare() {
  cd "ido-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001-Temporarily-disable-previous-button-drop-shadow-draw.patch
}

build() {
  cd "ido-${_actual_ver}${_extra_ver}"

  export CFLAGS+=" -Wno-error"
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static --disable-introspection
  make
}

package() {
  cd "ido-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}
