# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>
# Contributor: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=wily
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-sound
_actual_ver=12.10.2
_extra_ver=+15.10.20150915
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="A unified sound menu"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-sound"
license=(GPL)
depends=(accountsservice dconf libgee06 libnotify libpulse libxml2
         unity-control-center)
makedepends=(cmake gobject-introspection intltool vala)
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sound_${_actual_ver}${_extra_ver}.orig.tar.gz"
        com.ubuntu.sound.gschema.xml
        0001-Not-running-on-Ubuntu-Touch.patch
        0002-Remove-tests.patch
        0003-mpris2-Position-property-should-be-int64.patch
        libpulse.vapi)
sha512sums=('ae4c3844c9e505d72d3d15b967e6cbc956d7d0334af550d0daa2d5f0836668ca7371d58c5862358af2323f6b83e3c8d94c4442a3cdddb57a8f90e0fec8e37aa8'
            '04e662c9724f983c0b9129e13394937541949d481de3722e72046a68a49772707a145854a09bc7830e10ddd9bcf3b3095c9101fbc0c29fd03cfd267a074aee3c'
            'fa2bd98576249474db60ac8980421916ca56dfbf4cb18934a12a275a121ac34007ba6aa40d88f02ff6243b0a28b41b1edb893deb88b27020dd3590a40997f68e'
            '5a4c1b4feb999428c691cc6d7654a4a89567691e0819b57726113f9a3599881c5777446ae4dd4038ab6f0cc8fd55aa7c2943e460ab6ae356b0e4a80f65db712c'
            '64ec82b50f1db6e219516acee3dde924847a68f9173e43c7fb44b485d1a4217bac40c4c2e9a9b9c381afd4c69b64109275f8933dddf7e4a1890c7cbfdfa1b865'
            '5c8920490356e35f994de15959658d4f6665732aa6a3feba10090791b2cd2233d27cffbab2b29bb165d06f449e96c8a35fc4a0a161baaef81780b9f5585cf52c')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001-Not-running-on-Ubuntu-Touch.patch
  patch -p1 -i ../0002-Remove-tests.patch
  patch -p1 -i ../0003-mpris2-Position-property-should-be-int64.patch

  cp ../libpulse.vapi vapi/
  sed -i '/libpulse$/iposix' src/CMakeLists.txt
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  [[ -d build ]] || mkdir build
  pushd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
  popd
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}/build/"

  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/share/glib-2.0/schemas/"
  install -m644 "${srcdir}/com.ubuntu.sound.gschema.xml" \
                "${pkgdir}/usr/share/glib-2.0/schemas/"

  rm -r "${pkgdir}/usr/share/upstart/"

  sed -i 's/NotShowIn/OnlyShowIn/g' \
    "${pkgdir}/etc/xdg/autostart/indicator-sound.desktop"
}
