# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-sound
_actual_ver=12.10.2
_extra_ver=+16.04.20160502.1
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="A unified sound menu"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-sound"
license=(GPL)
depends=(accountsservice dconf libgee libnotify libpulse libxml2
         unity-control-center gsettings-ubuntu-schemas)
makedepends=(cmake gobject-introspection intltool vala unity-api)
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sound_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Not-running-on-Ubuntu-Touch.patch
        0002-Remove-tests.patch
        libpulse.vapi)
sha512sums=('304b2452c400b533a042b943a3e4ec9712de453aae23a597fa67f9f949d02214062ba2693fdcc335f7ac3f1e821c08af38c9de010a3bb82bd3e3391e7b03f476'
            'f7aa0a486f4831b2a4ddd5b5a894129075d8eb20995eeac79db8d6bb4f675eb885ff48c5bb7f81447f9fd1229648f67bc4c294f8692292397b95e73e4a43d464'
            '5a4c1b4feb999428c691cc6d7654a4a89567691e0819b57726113f9a3599881c5777446ae4dd4038ab6f0cc8fd55aa7c2943e460ab6ae356b0e4a80f65db712c'
            '5c8920490356e35f994de15959658d4f6665732aa6a3feba10090791b2cd2233d27cffbab2b29bb165d06f449e96c8a35fc4a0a161baaef81780b9f5585cf52c')

prepare() {
  cd .

  patch -p1 -i ../0001-Not-running-on-Ubuntu-Touch.patch
  patch -p1 -i ../0002-Remove-tests.patch

  cp ../libpulse.vapi vapi/
  sed -i '/libpulse$/iposix' src/CMakeLists.txt
}

build() {
  cd .

  [[ -d build ]] || mkdir build
  pushd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
  popd
}

package() {
  cd build

  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/share/upstart/"

  sed -i 's/NotShowIn/OnlyShowIn/g' \
    "${pkgdir}/etc/xdg/autostart/indicator-sound.desktop"
}
