# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-sound
_actual_ver=12.10.2
_extra_ver=+16.04.20160113.1
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="A unified sound menu"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-sound"
license=(GPL)
depends=(accountsservice dconf libgee06 libnotify libpulse libxml2
         unity-control-center)
makedepends=(cmake gobject-introspection intltool vala unity-api)
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sound_${_actual_ver}${_extra_ver}.orig.tar.gz"
        com.ubuntu.sound.gschema.xml
        0001-Not-running-on-Ubuntu-Touch.patch
        0002-Remove-tests.patch
        libpulse.vapi)
sha512sums=('e1734660edfd424d2f046de042041e1a65de156405b77a3ebd7f88742da25dd18ed63bdee78566ee1eecfa9518987065a536d261d591dd94048d2e7c80350212'
            '04e662c9724f983c0b9129e13394937541949d481de3722e72046a68a49772707a145854a09bc7830e10ddd9bcf3b3095c9101fbc0c29fd03cfd267a074aee3c'
            'e0b051509b14838072a4b01dd1ea3d3f169f89c6a5ec36d544edddba2b3f680858c91179062ed0942e5f2fb1d5ed3a7b88b71d8aaea78959992ada9dc9e44f85'
            '5a4c1b4feb999428c691cc6d7654a4a89567691e0819b57726113f9a3599881c5777446ae4dd4038ab6f0cc8fd55aa7c2943e460ab6ae356b0e4a80f65db712c'
            '5c8920490356e35f994de15959658d4f6665732aa6a3feba10090791b2cd2233d27cffbab2b29bb165d06f449e96c8a35fc4a0a161baaef81780b9f5585cf52c')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001-Not-running-on-Ubuntu-Touch.patch
  patch -p1 -i ../0002-Remove-tests.patch

  cp ../libpulse.vapi vapi/
  sed -i '/libpulse$/iposix' src/CMakeLists.txt
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  [[ -d build ]] || mkdir build
  pushd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
  popd
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}/build/"

  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/share/glib-2.0/schemas/"
  install -m644 "${srcdir}/com.ubuntu.sound.gschema.xml" \
                "${pkgdir}/usr/share/glib-2.0/schemas/"

  rm -r "${pkgdir}/usr/share/upstart/"

  sed -i 's/NotShowIn/OnlyShowIn/g' \
    "${pkgdir}/etc/xdg/autostart/indicator-sound.desktop"
}
