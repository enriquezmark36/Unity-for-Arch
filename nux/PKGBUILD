# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=nux
_actual_ver=4.0.8
_extra_ver=+16.10.20160705.1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=2
pkgdesc="Graphical user interface toolkit for applications that mixes OpenGL hardware acceleration with high quality visual rendering"
arch=(i686 x86_64)
url="https://launchpad.net/nux"
license=(LGPL)
depends=(gdk-pixbuf2 glew pango mesa libsigc++ libxcomposite libxdamage
         libxxf86vm geis ibus pciutils)
makedepends=(boost glproto dri2proto doxygen graphviz)

groups=(unity)
options=(!emptydirs)

install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/nux_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001_autoconf.patch
        50_check_unity_support)
sha512sums=('d50d7f772caa91b3d59b9be5c42cdecc3bc30fc23f172d646ddff3ce2bd5c61226aa845da1d4a0a95294387273e13009ef499d1d41f27888ac45b28a2412d968'
            '84d41b3ca21457f141d5203b1483f26190f161858d97176a04d0c2cafe6d01ea32f87fe6dc7cbe9234389e627669e34331ad400386733d5363d0145cbab63bb9'
            '1e82b11357f9db45fa6fff002cef11acaf53a8381e2bc28b84e9e0300502c0b1922d9273b44d4cfbbd51d004a9eb1eb2e780db7a39e2d9153c9c4efafb9314a9')

prepare() {
  if [ -d "${pkgname}-${_actual_ver}${_extra_ver}" ]; then
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
  fi

  patch -p1 -i ../0001_autoconf.patch
}

build() {
  if [ -d "${pkgname}-${_actual_ver}${_extra_ver}" ]; then
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
  fi

  export CFLAGS+=" -Wno-error=unused-function"
  export CXXFLAGS+=" -Wno-error=unused-function"

  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static

  make
}

package() {
  if [ -d "${pkgname}-${_actual_ver}${_extra_ver}" ]; then
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
  fi

  make DESTDIR="${pkgdir}" install

  rm -rv "${pkgdir}/usr/share/nux/gputests/"

  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m755 "${srcdir}/50_check_unity_support" \
                "${pkgdir}/etc/X11/xinit/xinitrc.d/"
}
