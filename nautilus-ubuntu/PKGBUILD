# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>
# Contributor: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase%-*}, repo=wily
# vercheck-archlinux: name=${pkgbase%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgbase%-*}, majorver=3.18
# vercheck-ppa: name=${pkgbase%-*}, url=ppa:gnome3-team/gnome3-staging

_use_ppa=true

pkgbase=nautilus-ubuntu
pkgname=(nautilus-ubuntu libnautilus-extension-ubuntu)
#_ubuntu_rel=0ubuntu13
#_ubuntu_ver=3.14.2
_ppa_rel=0ubuntu1~wily1
pkgver=3.18.1
pkgrel=2
pkgdesc="GNOME and Unity file manager"
arch=(i686 x86_64)
license=(GPL)
depends=(libexif gnome-desktop exempi gvfs desktop-file-utils dconf
         libtracker-sparql libnotify nautilus-sendto libunity libzeitgeist)
makedepends=(intltool gobject-introspection python python2)
url="http://www.gnome.org"
options=(!emptydirs)
install=nautilus.install
source=("http://ftp.gnome.org/pub/gnome/sources/nautilus/${pkgver%.*}/nautilus-${pkgver}.tar.xz")
if [[ "${_use_ppa}" == "true" ]]; then
    source+=("https://launchpad.net/~gnome3-team/+archive/ubuntu/gnome3-staging/+files/nautilus_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz")
else
    source+=("https://launchpad.net/ubuntu/+archive/primary/+files/nautilus_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
fi

sha512sums=('f0000cb9a53834ca1a7cc707ae1b3191cb450b32c141fd95026e9644b3374aa2dcc0b4216959938d08c95d71825d0af794e050cd62267f24b96a1722a4387ff9'
            '0d577fc5a55a521142316e04fd2f45678a9c9f8c7298767a2f8a51eb7ddb9c5c79564ff3e04cd0321cbbfe5825ab7ef8c57e19af456f7cc9df222ff70b5869d7')

prepare() {
  cd "${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches

  # Disable patches
  sed -i -e '/15_use-ubuntu-help.patch/d' \
         -e '/08_clean_session_capplet.patch/d' \
         ../debian/patches/series

  for i in $(grep -v '#' ../debian/patches/series); do
    msg "Applying ${i} ..."
    patch -p1 -i "../debian/patches/${i}"
  done

  sed -i '/gnome_bg_set_draw_background/d' libnautilus-private/nautilus-desktop-background.c
}

build() {
  cd "${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib/nautilus \
    --disable-update-mimedb \
    --disable-packagekit \
    --disable-schemas-compile \
    --disable-appindicator \
    --enable-unity

  make
}

package_nautilus-ubuntu() {
  depends+=(libnautilus-extension-ubuntu)
  groups=(gnome unity)
  provides=("nautilus=${pkgver}")
  conflicts=(nautilus)

  cd "nautilus-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # Split libnautilus-extension
  cd ..
  mkdir -p n-e/usr/{lib,share}
  mv "${pkgdir}"/usr/include n-e/usr
  mv "${pkgdir}"/usr/lib/{girepository-1.0,pkgconfig} n-e/usr/lib
  mv "${pkgdir}"/usr/lib/libnautilus-extension.so* n-e/usr/lib
  mv "${pkgdir}"/usr/share/{gir-1.0,gtk-doc} n-e/usr/share

  # Ubuntu specific stuff
  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  install -dm755 "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/nautilus.xpm" "${pkgdir}/usr/share/pixmaps/"
  install -m644 "${srcdir}/debian/mount-archive.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/nautilus-folder-handler.desktop" "${pkgdir}/usr/share/applications/"

  # Use language packs
  #rm -r "${pkgdir}/usr/share/locale/"

  # Make Unity happy
  cp "${pkgdir}"/usr/share/applications/{org.gnome.Nautilus,nautilus}.desktop
  sed -i \
      -e '/DBusActivatable/d' \
      -e '/MimeType/d' \
      "${pkgdir}"/usr/share/applications/nautilus.desktop
}

package_libnautilus-extension-ubuntu() {
  pkgdesc="Library for extending the ${pkgdesc}"
  depends=(cairo gtk3)
  provides=("libnautilus-extension=${pkgver}")
  conflicts=(libnautilus-extension)

  mv n-e/* "${pkgdir}/"
}
