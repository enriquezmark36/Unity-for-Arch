# Maintainer: Michael Healy <horsemanoffaith@gmail.com>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase%-*}, repo=xenial
# vercheck-archlinux: name=${pkgbase%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgbase%-*}, majorver=3.18
# vercheck-ppa: name=${pkgbase%-*}, url=ppa:gnome3-team/gnome3-staging

_use_ppa=true

pkgbase=nautilus-ubuntu
pkgname=(nautilus-ubuntu libnautilus-extension-ubuntu)
#_ubuntu_rel=0ubuntu1
#_ubuntu_ver=3.18.5
_ppa_rel=0ubuntu1~xenial1
pkgver=3.18.5
pkgrel=1
pkgdesc="GNOME and Unity file manager"
arch=(i686 x86_64)
license=(GPL)
depends=(libexif gnome-desktop exempi gvfs desktop-file-utils dconf
         libtracker-sparql libnotify nautilus-sendto libunity libzeitgeist)
makedepends=(intltool gobject-introspection python python2 zeitgeist-ubuntu)
url="http://www.gnome.org"
options=(!emptydirs)
install=nautilus.install
source=("http://ftp.gnome.org/pub/gnome/sources/nautilus/${pkgver%.*}/nautilus-${pkgver}.tar.xz")
if [[ "${_use_ppa}" == "true" ]]; then
    source+=("https://launchpad.net/~gnome3-team/+archive/ubuntu/gnome3-staging/+files/nautilus_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz")
else
    source+=("https://launchpad.net/ubuntu/+archive/primary/+files/nautilus_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
fi

sha512sums=('60ec5c1eb7ba90caed2f536a0fd78d8686b6f7372db36d526d634d727ac03719edee894ca0ff1d90a0974cc0bc76f05bf0f8d4db22845ae3585470aa8e7b8ca6'
            'deaf6a9252a715500b9e3b2d46e15ab2b28b18f49bdcc6e066dcc4d690f55f5bcbe09d7147a956516ad4621c8bc241164ea123db6cd9f2d50897831159bb7267')

prepare() {
  cd "${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches

  # Disable patches
  sed -i -e '/15_use-ubuntu-help.patch/d' \
         -e '/08_clean_session_capplet.patch/d' \
         ../debian/patches/series

  for i in $(grep -v '#' ../debian/patches/series); do
    msg "Applying ${i} ..."
    patch -p1 -i "../debian/patches/${i}"
  done

  sed -i '/gnome_bg_set_draw_background/d' libnautilus-private/nautilus-desktop-background.c
}

build() {
  cd "${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib/nautilus \
    --disable-update-mimedb \
    --disable-packagekit \
    --disable-schemas-compile \
    --disable-appindicator \
    --enable-unity

  make
}

package_nautilus-ubuntu() {
  depends+=(libnautilus-extension-ubuntu)
  groups=(gnome unity)
  provides=("nautilus=${pkgver}")
  conflicts=(nautilus)

  cd "nautilus-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # Split libnautilus-extension
  cd ..
  mkdir -p n-e/usr/{lib,share}
  mv "${pkgdir}"/usr/include n-e/usr
  mv "${pkgdir}"/usr/lib/{girepository-1.0,pkgconfig} n-e/usr/lib
  mv "${pkgdir}"/usr/lib/libnautilus-extension.so* n-e/usr/lib
  mv "${pkgdir}"/usr/share/{gir-1.0,gtk-doc} n-e/usr/share

  # Ubuntu specific stuff
  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  install -dm755 "${pkgdir}/usr/share/applications/"
    install -m644 "${srcdir}/debian/mount-archive.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/nautilus-folder-handler.desktop" "${pkgdir}/usr/share/applications/"

  # Use language packs
  #rm -r "${pkgdir}/usr/share/locale/"

  # Make Unity happy
  cp "${pkgdir}"/usr/share/applications/{org.gnome.Nautilus,nautilus}.desktop
  sed -i \
      -e '/DBusActivatable/d' \
      -e '/MimeType/d' \
      "${pkgdir}"/usr/share/applications/nautilus.desktop
}

package_libnautilus-extension-ubuntu() {
  pkgdesc="Library for extending the ${pkgdesc}"
  depends=(cairo gtk3-ubuntu)
  provides=("libnautilus-extension=${pkgver}")
  conflicts=(libnautilus-extension)

  mv n-e/* "${pkgdir}/"
}
