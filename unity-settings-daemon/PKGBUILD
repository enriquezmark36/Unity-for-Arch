# Maintainer: Michael Healy <horsemanoffaith@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety

pkgname=unity-settings-daemon
_actual_ver=15.04.1
_extra_ver=+16.10.20160615
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Unity Settings Daemon"
arch=(i686 x86_64)
url="https://launchpad.net/unity-settings-daemon"
license=(GPL)
groups=(unity)
depends=(gnome-settings-daemon-ubuntu gsettings-desktop-schemas-ubuntu fcitx
         gperf hicolor-icon-theme ibus libcanberra-pulse
         libnotify librsvg libsystemd libwacom libxkbfile mesa pulseaudio
         pulseaudio-alsa upower)
makedepends=(intltool xf86-input-wacom libxslt docbook-xsl python2)
conflicts=(gnome-desktop-compat upower-compat)
unity=settings-install-daemon.install
#options=('!emptydirs')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-settings-daemon_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Remove-accountsservice-dependency.patch
        0002-Add-gnome-settings-daemon-3.12-rfkill-plugin.patch
        0003-Remove-gsdscreensaver-undefined-refs.patch)
sha512sums=('4266c0ed8f9c89f3f3c24f6f8b76fbb4774eafab78b3c3c51e62b5a6820ebe9dcebe9c11110e6debda11592a2fe959ba72f407c562276d23be064429ef0795f6'
            'd842979cbb4d999d0af0f372cfa1a90aab65dd3aad02b26fc4420cf8a3e323cbf4614b3683b539e1068baa12bb8dc13aa3322d17d40447f5cb4dd9ebd07baf48'
            'd45d84e1ae0823ead75c780125a21635cc1732c640354cfdd54d986879eb5026b0bab0cf7562483bc81a7be0b386fb79d467ef5dca72f494358292aae23737d3'
            '4af29363028fc30310ab68583da12f336c8af6304ee08be57688f3eb48a29ecf0d0c178681481ab575dbdc5ba93c804d8b218380d8b3198ac519305eab84eb1a')

prepare() {
  if [ -d "${pkgname}-${_actual_ver}${_extra_ver}" ]; then
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
  fi
  
    patch -p1 -i ${srcdir}/0001-Remove-accountsservice-dependency.patch
    patch -p1 -i ${srcdir}/0002-Add-gnome-settings-daemon-3.12-rfkill-plugin.patch
    patch -p1 -i ${srcdir}/0003-Remove-gsdscreensaver-undefined-refs.patch

  # Temporarily disable since it fails to link to glib
  sed -i '/noinst_PROGRAMS += test-wm-button-layout-translations/d' plugins/xsettings/Makefile.am
}

build() {
  if [ -d "${pkgname}-${_actual_ver}${_extra_ver}" ]; then
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
  fi

  autoreconf -vfi
  intltoolize -f

  unset CFLAGS
  unset CXXFLAGS
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/unity-settings-daemon \
    --disable-static \
    --enable-systemd \
    --enable-fcitx

  # https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  if [ -d "${pkgname}-${_actual_ver}${_extra_ver}" ]; then
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
  fi
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/bin/"
  ln -s /usr/lib/unity-settings-daemon/unity-settings-daemon \
    "${pkgdir}/usr/bin/unity-settings-daemon"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}
