# Maintainer: Michael Healy: <horsemanoffaith@gmail.com>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=unity-lens-music
_actual_ver=6.9.1
_extra_ver=+16.04
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="Music, in the dash, Birllant."
arch=(i686 x86_64)
url="https://launchpad.net/unity-lens-music"
license=(GPL)
depends=(gstreamer gst-plugins-base libgee libnotify liboauth libsoup libunity
         tdb)
makedepends=(vala intltool json-glib)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
"https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz")

sha512sums=('fb97b3d6472fe2f97365c3fbd0864bab7f19aacaeb9cb3439a4bfc5a0506a26b8ecdb9389189db23549bcede288e04e6af6f6b7b58957ef35e0f8145cda23afc'
            'd263d94c6f689a6e881b24e24589cabde2fecd117aa856e0748b7e1de472b44d0bc195d377773f432dc3d5471354820b4c408bc5b5059689edf1df9e5d0dcb03')
                       
prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../unity-lens-music_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  aclocal --install --force
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}
