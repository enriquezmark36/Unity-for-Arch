# Maintainer: Michael Healy: <horsemanoffaith@gmail.com>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=unity-lens-music
_actual_ver=6.9.1
_extra_ver=+16.04
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
epoch=1
pkgdesc="Music, in the dash"
arch=(i686 x86_64)
url="https://launchpad.net/unity-place-music"
license=(GPL)
depends=(gstreamer gst-plugins-base libgee06 libnotify liboauth libsoup libunity
         tdb)
makedepends=(vala intltool json-glib)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-music_${_actual_ver}${_extra_ver}.orig.tar.gz"
     0001-Remove-Ubuntu-One-support-service-is-dead.patch)

sha512sums=('fb97b3d6472fe2f97365c3fbd0864bab7f19aacaeb9cb3439a4bfc5a0506a26b8ecdb9389189db23549bcede288e04e6af6f6b7b58957ef35e0f8145cda23afc'
            'd3f07b3ea42708ac3a323ecf827b6b43ee6682a9aa9df9f8be0e9071ac38adc2c289b79fb13eddce683ff6098655c5cf631ef9116119c5694299aace2e9f1bcd')
                        
prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001-Remove-Ubuntu-One-support-service-is-dead.patch
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  aclocal --install --force
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}
